function gpl --description="Auto prune and merge latest for the current branch"
  git pull --all --prune --tags
end

function ghb --description="Calculate and attempt to open a GitHub URL for the current branch"
  set URL (git remote get-url --push origin | sed 's/\.git$//g')/tree/(git rev-parse --abbrev-ref HEAD)
  echo "launching url $url..."
  if test (uname) = "Linux"
    xdg-open $URL
  else
    open $URL
  end
end

function glc --description="Calculate the most recent commit as a hyperlink and print it with an optional preamble"
  git rev-parse HEAD > /dev/null 2>&1
  if test "$status" -ne "0"
    # not a git repo
    set URL (pwd)
  else
    set URL (git remote get-url --push origin | sed 's/\.git$//g')
    set URL "$URL/commit/"(git rev-parse HEAD)
  end
  echo "$argv$URL"
end

function blame --description="Calculate a blame URL for one line or a range of lines"
  git rev-parse HEAD > /dev/null 2>&1
  if test "$status" -ne "0"
    # not a git repo
    set URL (pwd)
  else
    set URL (git remote get-url --push origin | sed 's/\.git$//g')
    set URL "$URL"/blame/(git rev-parse --abbrev-ref HEAD)
  end
  set URL "$URL/$argv[1]#L$argv[2]"

  if test "$argv[3]" !=  ""
    set URL "$URL-L$argv[3]"
  end

  open $URL
end

function fish_greeting --description="Override default behavior with a custom fish_greeting function"
  set -l is_darwin (test (uname) = "Darwin"; and echo true; or echo false)
  set -l is_local (test "$SSH_TTY" = ""; and echo true; or echo false)
  set -l is_vscode (test "$TERM_PROGRAM" = "vscode"; and echo true; or echo false)
  set -l has_neofetch (command -v neofetch >/dev/null 2>&1; and echo true; or echo false)
  set -l has_fastfetch (command -v fastfetch >/dev/null 2>&1; and echo true; or echo false) 

  if $is_vscode # need to detect this for jetbrains as well
    return
  end
  
  if $is_darwin; and $has_fastfetch
    if $is_local
      # suppress logo for SSH sessions
      fastfetch --logo-type small
    else
      fastfetch
    end
  else if $has_neofetch
    neofetch 
  end
end

function cal --description "Enhanced calendar with qtr, cy, eoy support + lolcat"
  set arg $argv[1]

  switch $arg
    case ''
      /usr/bin/cal | lolcat 2>/dev/null

    case qtr
      set m (date -u +%m)
      set mb (math "($m - 1) % 3")
      set mf (math "2 - $mb")
      set opts
      test $mb -gt 0; and set opts $opts -B$mb
      test $mf -gt 0; and set opts $opts -A$mf
      /usr/bin/cal $opts | lolcat 2>/dev/null

    case eoy
      set m (date -u +%m)
      set rem (math "12 - $m")
      /usr/bin/cal -A$rem | lolcat 2>/dev/null

    case cy
      /usr/bin/cal (date -u +%Y) | lolcat 2>/dev/null

    case '*'
      /usr/bin/cal $argv | lolcat 2>/dev/null
  end
end

